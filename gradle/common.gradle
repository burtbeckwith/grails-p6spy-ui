def versionTxt = file('version.txt')
version = versionTxt.exists() ? versionTxt.text.trim() : '0.1'

project.ext.setIfNotSet = { String name, value, boolean ext = true ->
	if (!project.ext.has(name) || !project[name]) {
		(ext ? project.ext : project)[name] = value
	}
}

setIfNotSet 'gradleWrapperVersion', '2.10'
setIfNotSet 'group', 'org.grails.plugins', false
setIfNotSet 'sourceCompatibility', 1.7
setIfNotSet 'targetCompatibility', 1.7

apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-gsp'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.grails.org/grails/core' }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {
	testCompile 'org.grails:grails-plugin-testing'
}

// deletes everything from the build directory except for test reports
task cleanBuild {
	if (!buildDir.exists()) return

	buildDir.eachFile {
		if (it.file) {
			it.delete()
		}
		else if (it.name != 'reports' && !it.name.startsWith('geb-reports') && !it.name.startsWith('test-results')) {
			it.deleteDir()
		}
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
		events 'failed', 'standardOut', 'standardError'
	}

	beforeTest { descriptor -> logger.quiet " -- $descriptor" }
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}
